dependencies {
  implementation project(':redis-smart-cache-jdbc')
  implementation 'org.springframework.boot:spring-boot-starter-cache'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'javax.cache:cache-api'
  implementation 'jakarta.xml.bind:jakarta.xml.bind-api'
  runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
  runtimeOnly group: 'org.webjars.npm', name: 'bootstrap', version: webjarsBootstrapVersion
  runtimeOnly group: 'org.webjars.npm', name: 'font-awesome', version: webjarsFontawesomeVersion
  runtimeOnly 'com.github.ben-manes.caffeine:caffeine'
  runtimeOnly 'com.mysql:mysql-connector-j'
  runtimeOnly 'org.postgresql:postgresql'
}

tasks.bootRun.dependsOn ':redis-smart-cache-jdbc:shadowJar'

tasks.bootJar.dependsOn ':redis-smart-cache-jdbc:shadowJar'

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}") { 
        into 'static'
    }
}

asciidoctor {
    forkOptions {
      // Fix issue with JDK16+
      jvmArgs("--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED")
    }
    resources {
    	from("$rootDir/src/media") {
     		include '**/*'
   		}
    }
    attributes \
    'icons': 'font',
    'docinfo1': '',
    'project-name': project.name,
    'project-title': config.info.description
}

config {
    publishing {
        enabled = false
    }
}

eclipse {
    project {
        name = 'redis-smart-cache-spring-petclinic'
    }
}
