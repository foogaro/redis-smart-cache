buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.kordamp.gradle:java-project-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:jacoco-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:kordamp-parentbuild:$kordampBuildVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$bootPluginVersion"
        classpath "gradle.plugin.com.github.johnrengelman:shadow:$shadowPluginVersion"
        classpath "me.champeau.jmh:jmh-gradle-plugin:$jmhPluginVersion"
        classpath "org.asciidoctor:asciidoctor-gradle-jvm:$asciidoctorVersion"
    }
}

apply plugin: 'org.kordamp.gradle.kordamp-parentbuild'

rootProject.name = 'redis-smart-cache'

projects {
    directories = ['core', 'demo']

    plugins {
        all {
            id 'eclipse'
        }
        path(':') {
            id 'org.kordamp.gradle.java-project'
        }
        dirs(['core', 'demo']) {
            id 'java-library'
            id 'org.springframework.boot'
            id 'io.spring.dependency-management'
            id 'org.asciidoctor.jvm.convert'
        }
        path(':redis-smart-cache-jdbc') {
            id 'com.github.johnrengelman.shadow'
            id 'me.champeau.jmh'
        }
        path('redis-smart-cache-cli') {
            id 'org.graalvm.buildtools'
        }
    }
}

enforce {
    mergeStrategy = 'append'
    rule(enforcer.rules.EnforceBytecodeVersion) { r ->
        // search only on compile and runtime classpaths
        enabled = false
        r.configurations.addAll(['compileClasspath', 'runtimeClasspath'])
        r.maxJdkVersion = '1.8'
    }
    rule(enforcer.rules.BanDuplicateClasses) { r ->
        // search only on compile and runtime classpaths
        r.configurations.addAll(['compileClasspath', 'runtimeClasspath'])
    }
    rule(enforcer.rules.ForceDependencies) { r ->
        r.dependencies.addAll "org.checkerframework:checker-qual:$checkerVersion",
            "com.redis:lettucemod:$lettucemodVersion"
    }
}