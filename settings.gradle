buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.kordamp.gradle:java-project-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:jacoco-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:kordamp-parentbuild:$kordampBuildVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$bootPluginVersion"
        classpath "io.github.goooler.shadow:shadow-gradle-plugin:$shadowPluginVersion"
        classpath "me.champeau.jmh:jmh-gradle-plugin:$jmhPluginVersion"
        classpath "org.asciidoctor:asciidoctor-gradle-jvm:$asciidoctorVersion"
    }
}

apply plugin: 'org.kordamp.gradle.kordamp-parentbuild'

rootProject.name = 'redis-smart-cache'

projects {
    directories = ['core', 'samples']

    plugins {
        all {
            id 'eclipse'
        }
        path(':') {
            id 'org.kordamp.gradle.java-project'
        }
        dirs(['core', 'samples']) {
            id 'java-library'
            id 'org.springframework.boot'
            id 'io.spring.dependency-management'
            id 'org.asciidoctor.jvm.convert'
        }
        path(':redis-smart-cache-jdbc') {
            id 'io.github.goooler.shadow'
            id 'me.champeau.jmh'
        }
        path('redis-smart-cache-cli') {
            id 'org.graalvm.buildtools'
        }
    }
}

enforce {
    rule(enforcer.rules.BanDuplicateClasses) { r ->
        // search only on compile and runtime classpaths
        r.configurations.addAll(['compileClasspath', 'runtimeClasspath'])
        r.enabled = false
    }
    rule(enforcer.rules.EnforceBytecodeVersion) { r ->
        r.enabled = false
    }
    rule(enforcer.rules.ForceDependencies) { r ->
        r.dependencies.addAll "org.checkerframework:checker-qual:$checkerVersion",
            "com.redis:lettucemod:$lettucemodVersion",
            "com.google.errorprone:error_prone_annotations:$errorproneVersion"
    }
}